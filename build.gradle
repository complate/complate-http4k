group 'com.github.complate'
version '0.1.1'
description "complate integration for HTTP4K"

buildscript {
  ext.kotlin_version = '1.1.51'

  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
  mavenCentral()
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
  compile group: "org.http4k", name: "http4k-core", version: '2.35.1'
  testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
  kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
  kotlinOptions.jvmTarget = "1.8"
}

jacocoTestReport {
  reports {
    xml.enabled true
    html.enabled false
  }
}

check.dependsOn jacocoTestReport

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'complate-http4k'
        packaging 'jar'

        description 'complate integration for HTTP4K'
        url 'http://github.com/complate/complate-http4k'

        organization {
          name 'com.github.complate'
          url '"https://github.com/complate"'
        }

        scm {
          connection 'scm:git:git@github.com:complate/complate-http4k.git'
          url 'https://github.com/complate/complate-http4k'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'cpelka'
            name 'Carsten Pelka'
            url 'https://github.com/cpelka'
          }
        }
      }
    }
  }
}
